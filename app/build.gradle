apply plugin: 'com.android.application'

android {
  buildToolsVersion '24.0.3'
  compileSdkVersion 24

  defaultConfig {
    applicationId 'pink.madis.perftesting.sample'
    versionName '1.0.0-jodatime-android'
    versionCode 1

    minSdkVersion 19
    targetSdkVersion 24
  }
}

dependencies {
  compile project(':reporter')
  compile 'net.danlew:android.joda:2.9.4.2'

  testCompile 'junit:junit:4.12'
}

android.applicationVariants.all { variant ->
  if (!variant.install) {
    // not installable, cannot measure startup either
    return
  }
  // attach our task to all installable variants
  task "measure${variant.name.capitalize()}Startup"(type: DefaultTask) {
    dependsOn variant.install
    doFirst {
      def adb = android.adbExecutable
      (0..40).each { repeatNr ->
        // kill the app
        println([
            adb, "shell", "am", "force-stop",
            variant.applicationId
        ].execute().text)

        // measure
        println([
            adb, "shell", "am", "start",
            "${variant.applicationId}/.MeasureActivity"
        ].execute().text)

        // wait for a while and repeat.
        // Ideally we would signal being done somehow instead, this needs to be
        // bigger if a startup takes longer than 250ms, otherwise we end up killing
        // our app mid-measurement
        Thread.sleep(250)
      }
    }
  }
}